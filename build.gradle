plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"
    id 'org.openapi.generator' version "$openApiGenVersion"
    id 'com.github.johnrengelman.shadow' version "$shadowJarVersion"
    id 'de.undercouch.download' version "$downloadPluginVersion"
    id 'java'
    id 'application'
    id 'idea'
}

// OpenAPI specifications (VREM & Cineast).
def specPath = "$rootDir/oas"

// Path for generated Cineast client sources.
def generatedPath = "$buildDir/generated"

// Path for Cineast OpenAPI specification.
def cineastOasPath = "$specPath/cineast-oas.json"

// VREM OpenAPI specification URL and local file.
def vremOasUrl = "http://localhost:4545/swagger-docs"
def vremOasPath = "$specPath/vrem-oas.json"

// Source/Target compatibility (currently used for source & target/JVM).
def javaCompatibility = 11

// Project settings.
group 'ch.unibas.dmi.dbis'
version '3.0.0'

// Main class.
mainClassName = 'ch.unibas.dmi.dbis.vrem.VREMKt'

// Only build the shadow jar and corresponding distributions.
jar.enabled = false
tasks.distZip.enabled = false
tasks.distTar.enabled = false

// Java properties.
java {
    sourceCompatibility = javaCompatibility
    targetCompatibility = javaCompatibility
}

// Application properties.
application {
    applicationDefaultJvmArgs = ["-Xms2G", "-Xmx4G"]
}

// Add generated code to sources.
sourceSets {
    main {
        java {
            srcDirs += "$generatedPath/src/main/kotlin/"
        }
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    archiveClassifier.set('') // Remove "-all" suffix.
}

// Generate Cineast OpenAPI bindings.
task generateCineastClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = 'kotlin'
    inputSpec = cineastOasPath
    packageName = "ch.unibas.dmi.dbis.vrem.cineast.client"
    outputDir = "$generatedPath"

    skipValidateSpec = true

    configOptions = [
            enumPropertyNaming: "UPPERCASE"
    ]
}

// Requires a VREM server running on port 4545.
task downloadVremOas(type: Download) {
    def f = new File(vremOasPath)
    src vremOasUrl
    dest f
}

task copyResources {
    doLast {
        copy {
            from "config.json"
            into "$buildDir/libs/"
        }
    }
}

task deploy {
    dependsOn generateCineastClient, build, copyResources
}

// Regenerate OpenAPI code before building.
compileKotlin {
    mustRunAfter generateCineastClient
    dependsOn generateCineastClient
    kotlinOptions.jvmTarget = javaCompatibility
}

compileTestKotlin {
    mustRunAfter generateCineastClient
    dependsOn generateCineastClient
    kotlinOptions.jvmTarget = javaCompatibility
}

// Regenerate OpenAPI code upon cleaning.
clean {
    finalizedBy generateCineastClient
}

// IntelliJ properties.
idea {
    module {
        // IntelliJ should download Javadoc.
        downloadJavadoc = true

        // Exclude directories from IntelliJ (indexing etc.).
        excludeDirs += file("data")
        excludeDirs += file("logs")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // CLI.
    implementation group: 'com.github.ajalt', name: 'clikt', version: "$cliktVersion"

    // Kotlinx Serialization.
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json', version: "$kotlinxSerializationVersion"

    // KMongo.
    implementation group: 'org.litote.kmongo', name: 'kmongo-serialization', version: "$kmongoVersion"
    implementation group: 'org.litote.kmongo', name: 'kmongo-id-serialization', version: "$kmongoVersion"

    // Javalin.
    implementation group: 'io.javalin', name: 'javalin-bundle', version: "$javalinVersion"

    // Jetty.
    implementation group: 'org.eclipse.jetty.http2', name: 'http2-server', version: "$jettyVersion"
    implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-server', version: "$jettyVersion"
    implementation group: 'org.eclipse.jetty.alpn', name: 'alpn-api', version: "$alpnApiVersion"
    implementation group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: "$alpnBootVersion"

    // Fuel.
    implementation group: 'com.github.kittinunf.fuel', name: 'fuel', version: "$fuelVersion"
    implementation group: 'com.github.kittinunf.fuel', name: 'fuel-kotlinx-serialization', version: "$fuelVersion"

    // OpenAPI Dependencies (OkHttp3 and Moshi Serialization).
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: "$okhttpVersion"
    implementation group: 'com.squareup.moshi', name: 'moshi-kotlin', version: "$moshiVersion"

    // Logging.
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "$log4jslf4jVersion"
    implementation group: 'io.github.microutils', name: 'kotlin-logging-jvm', version: "$kotlinLoggingVersion"

    // Kotlin test & JUnit library.
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: "$kotlinVersion"
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: "$kotlinVersion"
}
